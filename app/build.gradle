apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    signingConfigs {
        config {
            keyAlias 'entertech'
            keyPassword '123456'
            storeFile file('../entertech.jks')
            storePassword '123456'
        }
    }
    def PROPERTY_FILE_NAME = "version.properties"
    def BUILD_NAME = "build.number"

    def buildCode = getVersionCodeWithPropertyName(PROPERTY_FILE_NAME, BUILD_NAME)
    def appVersionCode = "1.1.2"
    compileSdkVersion 28
    defaultConfig {
        applicationId "cn.entertech.cushioncollector"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode buildCode
        versionName appVersionCode
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    productFlavors {
        flowtimezh {
            flavorDimensions "default"
            applicationId "cn.entertech.cushioncollector"
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/ble_release.kotlin_module'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation fileTree(dir: 'libs', include: ['*.aar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'cn.entertech.android:uicomponentsdk:1.2.10'
    implementation 'com.flyco.tablayout:FlycoTabLayout_Lib:2.1.2@aar'
    implementation 'cn.entertech.android:biomodulebleui:1.1.0'
    implementation 'cn.entertech.android:biomoduleble:1.4.0'
    implementation 'com.airbnb.android:lottie:3.0.7'
    implementation 'org.greenrobot:eventbus:3.1.1'

    implementation 'cn.entertech.android:affectivecloud:2.2.0'
    implementation 'com.orhanobut:logger:2.1.1'
    implementation 'org.slf4j:slf4j-api:1.7.7'
    implementation 'org.slf4j:slf4j-log4j12:1.7.7'

    implementation "io.reactivex.rxjava2:rxjava:2.2.7"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation("com.squareup.okhttp3:okhttp:3.14.0")
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.0'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.50'
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    implementation 'com.github.gzu-liyujiang.AndroidPicker:WheelPicker:1.3.1'
    implementation 'com.tencent.bugly:crashreport:latest.release' //其中latest.release指代最新Bugly SDK版本号，也可以指定明确的版本号，例如2.1.9
    implementation 'com.tencent.bugly:nativecrashreport:latest.release' //其中latest.release指代最新Bugly NDK版本号，也可以指定明确的版本号，例如3.0
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0-alpha'
    implementation 'com.google.android:flexbox:2.0.1'
    implementation 'cn.entertech.android:cushionble:1.0.0-alpha02'
}

// build 版本 +1
def getVersionCodeWithPropertyName(String fileName, String propertyName) {
    def runTasks = gradle.startParameter.taskNames
    def versionFile = file(fileName)
    if (versionFile.exists()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionFile))
        def versionCode = versionProps[propertyName].toInteger()
        if (!('assemble' in runTasks
                || 'assembleRelease' in runTasks
                || 'aR' in runTasks
                || 'assembleFlowtimezh' in runTasks)) {
            return versionCode
        } else {
            def code = versionCode + 1
            versionProps[propertyName] = code.toString()
            versionProps.store(versionFile.newWriter(), null)
            return code
        }
    } else {
        throw new GradleException("Could not find version.properties file, please check the file whether exist!")
    }
}
